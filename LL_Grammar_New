1. <PROG> -> <SEQUENCE> <PROG>
2. <PROG> -> FUNC IDENTIFIER LEFT_PAR <FIRST_PARAM_DEF> <DEF_FUNCTION> <PROG>
3. <PROG> -> LEX_EOF
4. <SEQUENCE> -> <VAR_DEF> IDENTIFIER <VAR_TYPE_OR_ASSIGN>
5. <SEQUENCE> -> IDENTIFIER <ASSIGN_OR_FUNCTION>
6. <SEQUENCE> -> IF <IF_EXP> LEFT_BRACKET <SEQUENCE_N> RIGHT_BRACKET <ELSE_STAT>
7. <SEQUENCE> -> WHILE <EXPRESSION> LEFT_BRACKET <SEQUENCE_N> RIGHT_BRACKET
8. <SEQUENCE_N> -> <SEQUENCE> <SEQUENCE_N>
9. <SEQUENCE_N> -> ε
10. <VAR_DEF> -> LET
11. <VAR_DEF> -> VAR
12. <VAR_TYPE_OR_ASSIGN> -> COLON <TYPE> <ASSIGN_VAR>
13. <VAR_TYPE_OR_ASSIGN> -> ASSIGNMENT <EXP_OR_CALL>
14. <ASSIGN_VAR> -> ASSIGNMENT <EXP_OR_CALL>
15. <ASSIGN_VAR> -> ε
16. <DEF_FUNCTION> -> <VOID_F> LEFT_BRACKET <SEQUENCE_N> <RETURN_FUNCTION> RIGHT_BRACKET
17. <VOID_F> -> ARROW <TYPE> 
18. <VOID_F> -> ε
19. <FIRST_PARAM_DEF> -> <PARAMS_DEF> <PARAMS_DEF_N>
20. <FIRST_PARAM_DEF> -> RIGHT_PAR
21. <PARAMS_DEF> -> <PARAMS_NAME_DEF> IDENTIFIER COLON <TYPE>
22. <PARAMS_NAME_DEF> -> UNDERSCORE
23. <PARAMS_NAME_DEF> -> IDENTIFIER
24. <PARAMS_DEF_N> -> RIGHT_PAR
25. <PARAMS_DEF_N> -> COMMA <PARAMS_DEF> <PARAMS_DEF_N>
26. <RETURN_FUNCTION> -> RETURN <RETURN_FUNCTION_N>
27. <RETURN_FUNCTION> -> ε     
28. <RETURN_FUNCTION_N> -> <EXPRESSION>
29. <RETURN_FUNCTION_N> -> ε
30. <ASSIGN_OR_FUNCTION> -> ASSIGNMENT <EXP_OR_CALL>
31. <ASSIGN_OR_FUNCTION> -> LEFT_PAR <FIRST_PARAM>
32. <EXP_OR_CALL> -> <EXPRESSION>
33. <EXP_OR_CALL> -> <CALL_FUNCTION>
34. <CALL_FUNCTION> -> IDENTIFIER LEFT_PAR <FIRST_PARAM> 
35. <FIRST_PARAM> -> RIGHT_PAR
36. <FIRST_PARAM> -> <PARAMS> <PARAMS_N>
37. <PARAMS_N> -> RIGHT_PAR
38. <PARAMS_N> -> COMMA <PARAMS> <PARAMS_N>
39. <PARAMS> -> INT_LIT
40. <PARAMS> -> DOUBLE_LIT
41. <PARAMS> -> STRING_LIT
42. <PARAMS> -> IDENTIFIER <PARAMS_NAME>
43. <PARAMS_NAME> -> COLON <ID_OR_LIT>
44. <PARAMS_NAME> -> ε
45. <ELSE_STAT> -> ELSE LEFT_BRACKET <SEQUENCE_N> RIGHT_BRACKET
46. <IF_EXP> -> <EXPRESSION>
47. <IF_EXP> -> LET IDENTIFIER
48. <TYPE> -> INT <Q_MARK>
49. <TYPE> -> DOUBLE <Q_MARK>
50. <TYPE> -> STRING <Q_MARK>
51. <Q_MARK> -> QUESTION_MARK  
52. <Q_MARK> -> ε
53. <ID_OR_LIT> -> IDENTIFIER
54. <ID_OR_LIT> -> INT_LIT
55. <ID_OR_LIT> -> DOUBLE_LIT
56. <ID_OR_LIT> -> STRING_LIT



<EXPRESSION> -> <EXPRESSION> * <EXPRESSION>
<EXPRESSION> -> <EXPRESSION> / <EXPRESSION>
<EXPRESSION> -> <EXPRESSION> + <EXPRESSION>
<EXPRESSION> -> <EXPRESSION> - <EXPRESSION>
<EXPRESSION> -> <EXPRESSION> == <EXPRESSION>
<EXPRESSION> -> <EXPRESSION> != <EXPRESSION>
<EXPRESSION> -> <EXPRESSION> < <EXPRESSION>
<EXPRESSION> -> <EXPRESSION> > <EXPRESSION>
<EXPRESSION> -> <EXPRESSION> <= <EXPRESSION>
<EXPRESSION> -> <EXPRESSION> >= <EXPRESSION>
<EXPRESSION> -> <EXPRESSION> ?? <EXPRESSION>
<EXPRESSION> -> <EXPRESSION> !
<EXPRESSION> -> LEFT_PAR <EXPRESSION> RIGHT_PAR        
<EXPRESSION> -> IDENTIFIER
<EXPRESSION> -> STRING_LIT
<EXPRESSION> -> INT_LIT
<EXPRESSION> -> DOUBLE_LIT
